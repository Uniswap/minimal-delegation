// SPDX-License-Identifier: MIT
pragma solidity ^0.8.23;

import {DelegationHandler} from "./utils/DelegationHandler.sol";
import {IERC7914} from "../src/interfaces/IERC7914.sol";
import {ERC7914} from "../src/ERC7914.sol";
import {BaseAuthorization} from "../src/BaseAuthorization.sol";
import {DeployPermit2} from "./utils/DeployPermit2.sol";
import {IPermit2} from "./interfaces/IPermit2.sol";
import {ISignatureTransfer} from "./interfaces/ISignatureTransfer.sol";
import {ERC20ETH} from "../lib/erc20-eth/src/ERC20Eth.sol";
import {IAllowanceTransfer} from "./interfaces/IAllowanceTransfer.sol";
import {SignatureVerification} from "./utils/SignatureVerification.sol";

contract ERC7914Test is DelegationHandler, DeployPermit2 {
    event TransferFromNative(address indexed from, address indexed to, uint256 value);
    event ApproveNative(address indexed owner, address indexed spender, uint256 value);
    event ApproveNativeTransient(address indexed owner, address indexed spender, uint256 value);
    event TransferFromNativeTransient(address indexed from, address indexed to, uint256 value);
    event NativeAllowanceUpdated(address indexed spender, uint256 value);

    address bob = makeAddr("bob");
    address recipient = makeAddr("recipient");

    function setUp() public {
        setUpDelegation();
    }

    function test_approveNative_revertsWithUnauthorized() public {
        vm.expectRevert(BaseAuthorization.Unauthorized.selector);
        signerAccount.approveNative(bob, 1 ether);
    }

    function test_approveNative_succeeds() public {
        vm.expectEmit(true, true, false, true);
        emit ApproveNative(address(signerAccount), bob, 1 ether);
        vm.prank(address(signerAccount));
        bool success = signerAccount.approveNative(bob, 1 ether);
        assertTrue(success);
        assertEq(signerAccount.nativeAllowance(bob), 1 ether);
    }

    /// forge-config: default.isolate = true
    /// forge-config: ci.isolate = true
    function test_approveNative_gas() public {
        vm.expectEmit(true, true, false, true);
        emit ApproveNative(address(signerAccount), bob, 1 ether);
        vm.startPrank(address(signerAccount));
        signerAccount.approveNative(bob, 1 ether);
        vm.snapshotGasLastCall("approveNative");
    }

    function test_transferFromNative_revertsWithIncorrectSender() public {
        vm.expectRevert(IERC7914.IncorrectSender.selector);
        signerAccount.transferFromNative(bob, recipient, 1 ether);
    }

    function test_transferFromNative_revertsWithAllowanceExceeded() public {
        vm.prank(address(signerAccount));
        bool success = signerAccount.approveNative(bob, 1 ether);
        assertTrue(success);
        vm.prank(bob);
        vm.expectRevert(IERC7914.AllowanceExceeded.selector);
        signerAccount.transferFromNative(address(signerAccount), bob, 2 ether);
    }

    function test_transferFromNative_zeroAmount_returnsTrue() public {
        vm.prank(address(signerAccount));
        bool success = signerAccount.approveNative(bob, 1 ether);
        vm.prank(bob);
        success = signerAccount.transferFromNative(address(signerAccount), bob, 0);
        assertEq(success, true);
    }

    function test_transferFromNative_succeeds() public {
        // send eth to signerAccount
        vm.deal(address(signerAccount), 1 ether);
        vm.prank(address(signerAccount));
        bool success = signerAccount.approveNative(bob, 1 ether);
        assertTrue(success);
        uint256 bobBalanceBefore = bob.balance;
        uint256 signerAccountBalanceBefore = address(signerAccount).balance;
        vm.expectEmit(true, true, false, true);
        emit NativeAllowanceUpdated(bob, 0 ether);
        vm.expectEmit(true, true, false, true);
        emit TransferFromNative(address(signerAccount), bob, 1 ether);
        vm.prank(bob);
        success = signerAccount.transferFromNative(address(signerAccount), bob, 1 ether);
        assertTrue(success);
        assertEq(signerAccount.nativeAllowance(bob), 0);
        assertEq(bob.balance, bobBalanceBefore + 1 ether);
        assertEq(address(signerAccount).balance, signerAccountBalanceBefore - 1 ether);
    }

    /// forge-config: default.isolate = true
    /// forge-config: ci.isolate = true
    function test_transferFromNative_gas() public {
        // send eth to signerAccount
        vm.deal(address(signerAccount), 1 ether);
        vm.prank(address(signerAccount));
        signerAccount.approveNative(bob, 1 ether);
        vm.expectEmit(true, true, false, true);
        emit TransferFromNative(address(signerAccount), bob, 1 ether);
        vm.prank(bob);
        signerAccount.transferFromNative(address(signerAccount), bob, 1 ether);
        vm.snapshotGasLastCall("transferFromNative");
    }

    function test_fuzz_transferFromNative(uint256 balance, uint256 approvedAmount, uint256 transferAmount) public {
        // ensure there are funds in the signerAccount
        vm.deal(address(signerAccount), balance);
        vm.prank(address(signerAccount));
        bool success = signerAccount.approveNative(bob, approvedAmount);
        assertEq(signerAccount.nativeAllowance(bob), approvedAmount);
        assertTrue(success);

        uint256 bobBalanceBefore = bob.balance;
        uint256 signerAccountBalanceBefore = address(signerAccount).balance;

        vm.prank(bob);
        // Check if the transfer amount is greater than the approved amount or the balance of the signerAccount
        // and expect the appropriate revert
        if (transferAmount > approvedAmount) {
            vm.expectRevert(IERC7914.AllowanceExceeded.selector);
        } else if (transferAmount > address(signerAccount).balance) {
            vm.expectRevert(IERC7914.TransferNativeFailed.selector);
        }
        success = signerAccount.transferFromNative(address(signerAccount), bob, transferAmount);
        // if the transfer was successful, check the balances have updated
        // otherwise check the balances have not changed
        if (success) {
            if (approvedAmount < type(uint256).max) {
                assertEq(signerAccount.nativeAllowance(bob), approvedAmount - transferAmount);
            } else {
                assertEq(signerAccount.nativeAllowance(bob), approvedAmount);
            }
            assertEq(bob.balance, bobBalanceBefore + transferAmount);
            assertEq(address(signerAccount).balance, signerAccountBalanceBefore - transferAmount);
        } else {
            assertEq(signerAccount.nativeAllowance(bob), approvedAmount);
            assertEq(bob.balance, bobBalanceBefore);
            assertEq(address(signerAccount).balance, signerAccountBalanceBefore);
        }
    }

    function test_approveNativeTransient_revertsWithUnauthorized() public {
        vm.expectRevert(BaseAuthorization.Unauthorized.selector);
        signerAccount.approveNativeTransient(bob, 1 ether);
    }

    function test_approveNativeTransient_succeeds() public {
        vm.expectEmit(true, true, false, true);
        emit ApproveNativeTransient(address(signerAccount), bob, 1 ether);
        vm.startPrank(address(signerAccount));
        bool success = signerAccount.approveNativeTransient(bob, 1 ether);
        assertTrue(success);
        assertEq(signerAccount.transientNativeAllowance(bob), 1 ether);
    }

    /// forge-config: default.isolate = true
    /// forge-config: ci.isolate = true
    function test_approveNativeTransient_gas() public {
        vm.expectEmit(true, true, false, true);
        emit ApproveNativeTransient(address(signerAccount), bob, 1 ether);
        vm.startPrank(address(signerAccount));
        signerAccount.approveNativeTransient(bob, 1 ether);
        vm.snapshotGasLastCall("approveNativeTransient");
    }

    function test_transferFromNativeTransient_revertsWithIncorrectSender() public {
        vm.expectRevert(IERC7914.IncorrectSender.selector);
        signerAccount.transferFromNativeTransient(bob, recipient, 1 ether);
    }

    function test_transferFromNativeTransient_revertsWithAllowanceExceeded() public {
        vm.prank(address(signerAccount));
        bool success = signerAccount.approveNativeTransient(bob, 1 ether);
        assertTrue(success);
        vm.prank(bob);
        vm.expectRevert(IERC7914.AllowanceExceeded.selector);
        signerAccount.transferFromNativeTransient(address(signerAccount), bob, 2 ether);
    }

    function test_transferFromNativeTransient_zeroAmount_returnsTrue() public {
        vm.prank(address(signerAccount));
        bool success = signerAccount.approveNativeTransient(bob, 1 ether);
        assertTrue(success);
        success = signerAccount.transferFromNativeTransient(address(signerAccount), bob, 0);
        assertEq(success, true);
    }

    function test_transferFromNativeTransient_succeeds() public {
        // send eth to signerAccount
        vm.deal(address(signerAccount), 1 ether);

        vm.prank(address(signerAccount));
        bool success = signerAccount.approveNativeTransient(bob, 1 ether);
        assertTrue(success);

        uint256 bobBalanceBefore = bob.balance;
        uint256 signerAccountBalanceBefore = address(signerAccount).balance;

        vm.expectEmit(true, true, false, true);
        emit TransferFromNativeTransient(address(signerAccount), bob, 1 ether);

        vm.prank(bob);
        success = signerAccount.transferFromNativeTransient(address(signerAccount), bob, 1 ether);
        assertTrue(success);

        assertEq(signerAccount.transientNativeAllowance(bob), 0);
        assertEq(bob.balance, bobBalanceBefore + 1 ether);
        assertEq(address(signerAccount).balance, signerAccountBalanceBefore - 1 ether);
    }

    function test_fuzz_transferFromNativeTransient_succeeds(
        uint256 balance,
        uint256 approvedAmount,
        uint256 transferAmount
    ) public {
        // ensure there are funds in the signerAccount
        vm.deal(address(signerAccount), balance);
        vm.prank(address(signerAccount));
        bool success = signerAccount.approveNativeTransient(bob, approvedAmount);
        assertEq(signerAccount.transientNativeAllowance(bob), approvedAmount);
        assertTrue(success);

        uint256 bobBalanceBefore = bob.balance;
        uint256 signerAccountBalanceBefore = address(signerAccount).balance;

        vm.prank(bob);
        // Check if the transfer amount is greater than the approved amount or the balance of the signerAccount
        // and expect the appropriate revert
        if (transferAmount > approvedAmount) {
            vm.expectRevert(IERC7914.AllowanceExceeded.selector);
        } else if (transferAmount > address(signerAccount).balance) {
            vm.expectRevert(IERC7914.TransferNativeFailed.selector);
        }
        success = signerAccount.transferFromNativeTransient(address(signerAccount), bob, transferAmount);
        // if the transfer was successful, check the balances have updated
        // otherwise check the balances have not changed
        if (success) {
            if (approvedAmount < type(uint256).max) {
                assertEq(signerAccount.transientNativeAllowance(bob), approvedAmount - transferAmount);
            } else {
                assertEq(signerAccount.transientNativeAllowance(bob), approvedAmount);
            }
            assertEq(bob.balance, bobBalanceBefore + transferAmount);
            assertEq(address(signerAccount).balance, signerAccountBalanceBefore - transferAmount);
        } else {
            assertEq(signerAccount.transientNativeAllowance(bob), approvedAmount);
            assertEq(bob.balance, bobBalanceBefore);
            assertEq(address(signerAccount).balance, signerAccountBalanceBefore);
        }
    }

    // Test that a permit2 signature can be used to transfer native ETH
    // using the ERC20-eth contract
    function test_permit2SignatureTransferNative() public {
        // Deploy ERC20ETH and Permit2
        ERC20ETH erc20Eth = new ERC20ETH();
        IPermit2 permit2 = IPermit2(deployPermit2());


        // Give signerAccount some ETH
        uint256 spendAmount = 1 ether;
        uint256 totalAmount = 2 ether;
        vm.deal(address(signerAccount), totalAmount);

        // Approve ERC20ETH to use signerAccount's native ETH
        vm.prank(address(signerAccount));
        signerAccount.approveNative(address(erc20Eth), type(uint256).max);
        
        // Create permit details
        ISignatureTransfer.PermitTransferFrom memory permit = ISignatureTransfer.PermitTransferFrom({
            permitted: ISignatureTransfer.TokenPermissions({
                token: address(erc20Eth),
                amount: spendAmount
            }),
            nonce: 0,
            deadline: block.timestamp + 1 hours
        });

        bytes memory sig = getPermitTransferSignature(permit, signerPrivateKey, permit2.DOMAIN_SEPARATOR(), bob);

        // Bob cannot transfer more than the approved amount
        ISignatureTransfer.SignatureTransferDetails memory invalidTransferDetails = ISignatureTransfer.SignatureTransferDetails({
            to: bob,
            requestedAmount: spendAmount + 1
        });

        vm.expectRevert(abi.encodeWithSelector(ISignatureTransfer.InvalidAmount.selector, spendAmount));
        vm.prank(bob);
        permit2.permitTransferFrom(permit, invalidTransferDetails, address(signerAccount), sig);

        // Bob can transfer the approved amount
        ISignatureTransfer.SignatureTransferDetails memory validTransferDetails = ISignatureTransfer.SignatureTransferDetails({
            to: bob,
            requestedAmount: spendAmount
        });
        vm.prank(bob);
        permit2.permitTransferFrom(permit, validTransferDetails, address(signerAccount), sig);

        // Verify the transfer
        assertEq(bob.balance, spendAmount);
        assertEq(address(signerAccount).balance, totalAmount - spendAmount);
    }

    bytes32 public constant _TOKEN_PERMISSIONS_TYPEHASH = keccak256("TokenPermissions(address token,uint256 amount)");
    bytes32 public constant _PERMIT_TRANSFER_FROM_TYPEHASH = keccak256(
        "PermitTransferFrom(TokenPermissions permitted,address spender,uint256 nonce,uint256 deadline)TokenPermissions(address token,uint256 amount)"
    );

    function getPermitTransferSignature(
        ISignatureTransfer.PermitTransferFrom memory permit,
        uint256 privateKey,
        bytes32 domainSeparator,
        address spender
    ) internal pure returns (bytes memory sig) {
        bytes32 tokenPermissions = keccak256(abi.encode(_TOKEN_PERMISSIONS_TYPEHASH, permit.permitted));
        bytes32 msgHash = keccak256(
            abi.encodePacked(
                "\x19\x01",
                domainSeparator,
                keccak256(
                    abi.encode(
                        _PERMIT_TRANSFER_FROM_TYPEHASH, tokenPermissions, spender, permit.nonce, permit.deadline
                    )
                )
            )
        );

        (uint8 v, bytes32 r, bytes32 s) = vm.sign(privateKey, msgHash);
        return bytes.concat(r, s, bytes1(v));
    }
}
